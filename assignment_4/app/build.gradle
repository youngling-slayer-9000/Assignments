plugins {
    id 'application'
    id 'java'
    id 'pmd'
    id 'com.github.spotbugs' version '6.0.7'
}


group = 'org.example'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    // JUnit 5
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

/* -------------------- JUnit -------------------- */

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = true
    }
}

/* -------------------- PMD -------------------- */

pmd {
    consoleOutput = true
    toolVersion = "7.0.0"
    rulesMinimumPriority = 5
    ignoreFailures = true

    ruleSets = [
            'category/java/errorprone.xml',
            'category/java/bestpractices.xml'
    ]
}


/* -------------------- SpotBugs -------------------- */
spotbugs {
    ignoreFailures = true
    showStackTraces = true
    showProgress = true
    effort = 'max'
    reportLevel = 'low'
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/${name}.html")
        }
        xml.required = false
    }
}

/* -------------------- Application -------------------- */

application {
    mainClass = 'lisp.Main'
}

/* -------------------- Java -------------------- */

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
